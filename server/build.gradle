/*******************************************************************************************
 *                              gradle file : SERVER
 ******************************************************************************************/


dependencies {
    compile project(':common')

    if (project.properties.containsKey("client") && "ng2".equals(client)) {
        compile project(':client-ng2')
    }
    else{
        compile project(':client-ng1')
    }
}

springBoot {
    mainClass = 'org.mixit.cesar.CesarApplication'
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = file("$buildDir/classes/main/")
    }
}

jar{
    version ""
}

/*
 * Gets the version name from the latest Git tag
 */
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

processResources {
    filesMatching('**/application.yml') {
        filter org.apache.tools.ant.filters.ReplaceTokens, tokens: [
                version: getVersionName()
        ]
    }
}